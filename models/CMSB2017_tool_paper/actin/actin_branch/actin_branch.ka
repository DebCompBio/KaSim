#############################################
##  Model of actin assembly and branching at steady state, based on
##  Roland, et al 2008 Biophys J. 94:2082-94 and Beltzner and Polard,
##  2008 J. of Bio. Chem. 283,11:7135-44, with additions to model
##  the capping protein
##
##  written by Michael Sneddon, October 2009

    # The pool of free Actin monomer agents
%init: 100   Actin(barbed,pointed,p~ATP,d,c~OPEN,vca,arp,state~ALIVE)

    # The pool of free ADF/cofilin agents
%init: 100   ADF(a)

    # Seed nucleation site for the polymer to start growth.  This
    # ensures that only one polymer starts forming per simulation.  You could add more, or
    # you could just run multiple simulations
    %init: 100  Actin(barbed,pointed!1,p~ATP,d,c~OPEN,vca,arp,state~ALIVE), Actin(barbed!1,pointed,p~ATP,d,c~OPEN,vca,arp,state~ALIVE)

    # the pool of Arp2/3 and activating factor VCA
    %init: 100 VCA(a,arp)
    %init: 100 ARP(f,vca,activated~NO)


    # Generator and trash agents to remove fragments and replenish
    # the pool of Actin and ADF/cofilin in the simulation
    %init: 100 Generator()
    %init: 100 Trash()


   #############################################
   # ELONGATION REACTIONS (barbed end)
   ###### MONOMER ###### + #### FILAMENT ################
   # Barbed end elongation :\
   Actin(barbed,pointed,vca), Actin(barbed,pointed!_,c~OPEN,state~ALIVE) -> Actin(barbed,pointed!1,vca), Actin(barbed!1,pointed!_,c~OPEN,state~ALIVE) @0{1}
   Actin(barbed,pointed!1,p~ATP,c~OPEN,state~ALIVE), Actin(barbed!1,pointed!_,vca) -> Actin(barbed,pointed,p~ATP,c~OPEN,state~ALIVE), Actin(barbed,pointed!_,vca) @1
   Actin(barbed,pointed!1,p~ADPp,c~OPEN,state~ALIVE), Actin(barbed!1,pointed!_,vca) -> Actin(barbed,pointed,p~ADPp,c~OPEN,state~ALIVE), Actin(barbed,pointed!_,vca) @1
   Actin(barbed,pointed!1,p~ADP,c~OPEN,state~ALIVE), Actin(barbed!1,pointed!_,vca) -> Actin(barbed,pointed,p~ADP,c~OPEN,state~ALIVE), Actin(barbed,pointed!_,vca) @1


   #############################################
   # ELONGATION REACTIONS (pointed end)
   ###### FILAMENT ############## + #### MONOMER ########
   #Pointed end elongation :\
   Actin(barbed!_,pointed,c~OPEN,vca,state~ALIVE), Actin(barbed,pointed,vca) -> Actin(barbed!_,pointed!1,c~OPEN,vca,state~ALIVE), Actin(barbed!1,pointed,vca) @0{1}
   Actin(barbed!_,pointed!1), Actin(barbed!1,pointed,p~ATP,c~OPEN,vca,state~ALIVE)  ->  Actin(barbed!_,pointed), Actin(barbed,pointed,p~ATP,c~OPEN,vca,state~ALIVE)@1
   Actin(barbed!_,pointed!1), Actin(barbed!1,pointed,p~ADPp,c~OPEN,vca,state~ALIVE) ->  Actin(barbed!_,pointed), Actin(barbed,pointed,p~ADPp,c~OPEN,vca,state~ALIVE) @1
   Actin(barbed!_,pointed!1), Actin(barbed!1,pointed,p~ADP,c~OPEN,vca,state~ALIVE)  ->  Actin(barbed!_,pointed), Actin(barbed,pointed,p~ADP,c~OPEN,vca,state~ALIVE)@1


   #############################################
   # ATP HYDROLYSIS REACTIONS
   # first order ATP hydrolysis reactions, with the phosphate dissassociation
   # rate dependent on the presence of a bound ADF on the filament
   Actin(p~ATP,d), Actin() -> Actin(p~ADPp,d), Actin()   @1
   Actin(p~ADPp,d), Actin() -> Actin(p~ADP,d), Actin()   @1
   Actin(p~ADPp,d), ADF() -> Actin(p~ADP,d), ADF() @1


   #############################################
   ### ADF/COFILIN BINDING REACTIONS
   # initial binding of ADF/cofilin, requires neighboring Actin subunits to be free of ADF
   Actin(p~ADP,d,barbed!1,pointed!2), Actin(pointed!1,d), Actin(barbed!2,d), ADF(a) -> Actin(p~ADP,d!3,barbed!1,pointed!2), Actin(pointed!1,d), Actin(barbed!2,d), ADF(a!3) @1

   # cooperative binding of ADF/cofilin to neighboring subunits, either through the
   # barbed end or through the pointed end - we need one rule for each
   Actin(d!_,pointed!1), Actin(p~ADP,d,barbed!1,pointed!_), ADF(a) -> Actin(d!_,pointed!1), Actin(p~ADP,d!2,barbed!1,pointed!_), ADF(a!2) @1
   Actin(p~ADP,d,barbed!_,pointed!1), Actin(d!_,barbed!1), ADF(a) -> Actin(p~ADP,d!2,barbed!_,pointed!1), Actin(d!_,barbed!1), ADF(a!2) @1

   #Unbinding of ADF
   Actin(d!1), ADF(a!1) -> Actin(d), ADF(a) @1



   #############################################
   ### SEVERING REACTIONS
   # severing reaction that also immediately kills the barbed end of the fragment
   # so that it can be discarded
   #Severing :\
   Actin(d!_, pointed!1), Actin(d!_,barbed!1,state~ALIVE) -> Actin(d!_,pointed), Actin(d!_,barbed,state~DEAD) @1





   #############################################
   ### BRANCHING REACTIONS

   # binding of VCA to ACTIN monomer is treated as instant (so that we don't have to model
   # binding and unbinding of VCA to ACTIN.  We simply create as much VCA as there are VCA-Actin)
   VCA(a,arp), Actin(barbed,pointed,vca) -> VCA(a!1,arp), Actin(barbed,pointed,vca!1) @1{0}

   # VCA-Actin binding to ARP2/3
   VCA(a!_,arp), ARP(vca) -> VCA(a!_,arp!2), ARP(vca!2) @1{0}
   VCA(a!_,arp!2), ARP(f,vca!2) -> VCA(a,arp), ARP(f,vca) @1


   # ARP2/3 binding to a filament
   ARP(f), Actin(barbed!_,pointed!_,arp) -> ARP(f!1), Actin(barbed!_,pointed!_,arp!1) @1{0}
   ARP(f!1,vca), Actin(barbed!_,pointed!_,arp!1) -> ARP(f,vca), Actin(barbed!_,pointed!_,arp) @1
   ARP(f!1,vca!_,activated~NO), Actin(barbed!_,pointed!_,arp!1) -> ARP(f,vca!_,activated~NO), Actin(barbed!_,pointed!_,arp) @1


   ## activated ARP 2/3 complex with an actin monomer can start polymerizing
   ## with the usual rate off of the barbed end
   #start poly branch :\
   ARP(vca!1,activated~YES), VCA(arp!1,a!2), Actin(vca!2,barbed,pointed,c~OPEN), Actin(barbed,pointed,vca) -> ARP(vca!1,activated~YES), VCA(arp!1,a!2), Actin(vca!2,barbed!4,pointed,c~OPEN), Actin(barbed,pointed!4,vca)@1


   #############################################
   ### ACTIVATING THE COMPLEX
   ARP(f!_,vca!_,activated~NO) -> ARP(f!_,vca!_,activated~YES) @1




   #############################################
   ### CAPPING REACTIONS, at the barbed end
   #Capping:\
   Actin(barbed,pointed!_,c~OPEN) -> Actin(barbed,pointed!_,c~CAPPED)  @1
   #Uncapping:\
   Actin(barbed,pointed!_,c~CAPPED) -> Actin(barbed,pointed!_,c~OPEN) @1


   #############################################
   ### RECYCLING REACTIONS
   # recycling reactions that discard CAPPED fragments, and add to the pool of free
   # Actin monomers to maintain Actin concentration in the volume and free ADF to
   # maintain the correct free ADF conncentration

   # Here we add new Actin and ADF molecules to replace those that are depleted
   Generator() -> Generator(), Actin(barbed,pointed,p~ATP,d,c~OPEN,vca,arp,state~ALIVE) @1
   Generator() -> Generator(), ADF(a) @1
   Generator() -> Generator(), VCA(a,arp) @1
   Generator() -> Generator(), ARP(f,vca,activated~NO) @1

   # Delete fragments that are capped and any ADP-actin monomers we can't use anymore
   Actin(state~DEAD) -> Trash()  @1
   Actin(barbed,pointed,p~ADP,arp,vca) -> Trash()  @1
   Actin(barbed,pointed,p~ADPp,arp,vca) -> Trash() @1
