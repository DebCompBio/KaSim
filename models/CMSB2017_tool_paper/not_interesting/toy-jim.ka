%var: 'kpL' 0.1
%var: 'kmL'    0.1
# kpD and kmD were divided by two from their values in toy.in
# give the right symmetry factor.
%var: 'kpD'    1.0
%var: 'kmD'    0.1
%var: 'kpA'    0.1
%var: 'kmA'    0.1
%var: 'kpK'    0.1
%var: 'kmK'    0.1
%var: 'pK'     1
%var: 'pKs'    10
%var: 'dM'     1
%var: 'dC'     10

    # Set to zero for equilibration
%init: 100     L(r)
    # r binds to l of R

%init: 100 R(l,r,a)
    # l binds to r of L
    # r binds to r of R

%init: 100     A(r,k)
    # r binds to a of R
    # k binds to a of K

%init: 100 K(a,Y~U)
    # a binds to k of A
    # Y is phosphorylation site that is either unphosphorylated (U) or
    #   phosphorylated (P)

%init: 100 Null()

    # Ligand binding (L+R)
    # Note: specifying r in R here means that the r component must not
    #       be bound.  This prevents dissociation of ligand from R
    #       when R is in a dimer.
    L(r) , R(l,r) <-> L(r!1), R(l!1,r) @'kpL'{0},'kmL'

    # Aggregation (R+R)
    # Note:  R must be bound to ligand to dimerize.
    L(r!1), R(l!1,r) , L(r!2), R(l!2,r) <-> L(r!1), R(l!1,r!3), L(r!2), R(l!2,r!3) @'kpD','kmD'

    # Receptor binding to adaptor (R+A)
    # Note: A and R can bind independent of whether A is bound to K or
    #       whether R is in a dimer.
    A(r), R(a) <-> A(r!1), R(a!1) @'kpA'{0},'kmA'

    # Adaptor binding kinase
    # Note: Doesn't depend on phosphorylation state of K or whether A is bound to
    #       receptor, i.e. binding rate is same whether A is on membrane (bound to
    #        R) or in cytosol.
    A(k) , K(a) <-> A(k!1), K(a!1) @'kpA'{0},'kmA'

    # Kinase transphosphorylation by inactive kinase
    # Note: Rule doesn't specify how two K's are associated
    K(Y~U), K(Y~U) -> K(Y~U), K(Y~P) @'pK'

    # Kinase transphosphorylation by active kinase
    # Note: Rule doesn't specify how two K's are associated
    K(Y~P), K(Y~U) -> K(Y~P), K(Y~P) @'pKs'

    # Dephosphorylation of kinase in membrane complex
    R(a!1), A(r!1,k!2), K(a!2,Y~P) -> R(a!1),A(r!1,k!2), K(a!2,Y~U) @'dM'

    # Dephosphorylation of kinase in cytosol
    K(a,Y~P) -> K(a,Y~U) @'dC'
