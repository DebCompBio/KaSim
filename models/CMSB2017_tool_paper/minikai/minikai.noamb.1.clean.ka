/* MWC cycle in the Kai system */

# WF 02/27/2017

/*
constants
*/

%var: 'Avogadro'         6.0221413E+23 # Avogadro's number
%var: 'R'                0.008314      # gas constant in kJ/(K mol)
%var: 'VSynechococcus'   2.8E-09       # in litres (2512 um x 1118 um on average)
%var: 'Vhypothetical'    2.8E-15       # in litres (2512 um x 1118 um on average)

/*
basic parameters
*/

%var: 'scale'     0.5
#%var: 'V'        'scale' * 'VSynechococcus' # model volume
%var: 'V'         'scale' * 'Vhypothetical' # model volume

/*
derived constants
*/

%var:   'stoch'   ('V'*'Avogadro')

/*
signatures
*/

%agent: p(l,r)
%agent: KaiC(x1~u~p,x2~u~p,x3~u~p,x4~u~p,x5~u~p,x6~u~p,s~active~inactive,a,b1,b2,p,tag)
%agent: KaiA(c,b)
%agent: KaiB(c,a)
%agent: Test(c)

/*
init
*/

#%init:  0 KaiA(c,b)
#%init:  0 KaiB(c,a)

%init:  ((0.58E-06 * 'stoch')) KaiA(c,b)  # quantity in mol
%init:  ((1.75E-06 * 'stoch')) KaiB(c,a)
%init:  ((0.58E-06 * 'stoch')) KaiC(x1~u,x2~u,x3~u,x4~u,x5~u,x6~u,s~active,a,b1,b2,tag,p!0),p(l!0,r)

#%init:  1 KaiC(x1~u,x2~u,x3~u,x4~u,x5~u,x6~u,s~active,a,b1,b2,tag!1,p!0),p(l!0,r),Test(c!1)

/*
observables
*/
/*
%var: 'p0' |KaiC(tag!10,p!0),p(l!0,r),Test(c!10)|
%var: 'p1' |KaiC(tag!10,p!0),p(l!0,r!1),p(l!1,r),Test(c!10)|
%var: 'p2' |KaiC(tag!10,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r),Test(c!10)|
%var: 'p3' |KaiC(tag!10,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r),Test(c!10)|
%var: 'p4' |KaiC(tag!10,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r),Test(c!10)|
%var: 'p5' |KaiC(tag!10,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r),Test(c!10)|
%var: 'p6' |KaiC(tag!10,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r!6),p(l!6,r),Test(c!10)|

%obs: 'pirbo' 'p1'+2*'p2'+3*'p3'+4*'p4'+5*'p5'+6*'p6'
%obs: 'pirboX' |KaiC(tag!1,s~active),Test(c!1)|
*/

%var: 'p0' |KaiC(p!0),p(l!0,r)|
%var: 'p1' |KaiC(p!0),p(l!0,r!1),p(l!1,r)|
%var: 'p2' |KaiC(p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r)|
%var: 'p3' |KaiC(p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r)|
%var: 'p4' |KaiC(p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r)|
%var: 'p5' |KaiC(p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r)|
%var: 'p6' |KaiC(p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r!6),p(l!6,r)|

%obs: '<p>' ('p1'+2*'p2'+3*'p3'+4*'p4'+5*'p5'+6*'p6')/(6*|KaiC()|)
%plot: 'p0'
%plot: 'p1'
%plot: 'p2'
%plot: 'p3'
%plot: 'p4'
%plot: 'p5'
%plot: 'p6'

/*
%obs: 'active' 	 |KaiC(s~active)|
%obs: 'inactive' |KaiC(s~inactive)|
*/
/*
%obs: 'BC' |KaiC(b1!_)|+|KaiC(b2!_)|+|KaiC(b1!_,b2!_)|
%obs: 'AC' |KaiC(a!_)|
*/

/*
spontaneous phosphorylation and dephosphorylation of (in)active KaiC (no KaiA bound)

The phosphorylation rate must be lower than the dephosphorylation rate.

Here we deploy a counter construct to keep track of global hexamer state.
Note that these rules apply also to KaiB-bound and KaiA.KaiB-bound hexamers
*/

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# The values of these rate constants are not free and have to be carefully chosen,
# because their sum determines the relaxation rate of the phosphorylation level of
# KaiC (in the absence of KaiA), while their ratio determines the steady-state
# phosphorylation level of KaiC (in the absence of KaiA).

%var: 'kpplus'  	 0.025	# h^{-1}  [original]
%var: 'kpminus'  	 0.4 	# h^{-1}  [original]
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

#------------------------------------------------------------------------------
'pCa_1'		KaiC(x1~u,a,p!0),p(l!0) <-> \
			KaiC(x1~p,a,p!0),p(l!1),p(l!0,r!1) @ 'kpplus', 'kpminus'
'pCa_2'		KaiC(x2~u,a,p!0),p(l!0) <-> \
			KaiC(x2~p,a,p!0),p(l!1),p(l!0,r!1) @ 'kpplus', 'kpminus'
'pCa_3'		KaiC(x3~u,a,p!0),p(l!0) <-> \
			KaiC(x3~p,a,p!0),p(l!1),p(l!0,r!1) @ 'kpplus', 'kpminus'
'pCa_4'		KaiC(x4~u,a,p!0),p(l!0) <-> \
			KaiC(x4~p,a,p!0),p(l!1),p(l!0,r!1) @ 'kpplus', 'kpminus'
'pCa_5'		KaiC(x5~u,a,p!0),p(l!0) <-> \
			KaiC(x5~p,a,p!0),p(l!1),p(l!0,r!1) @ 'kpplus', 'kpminus'
'pCa_6'		KaiC(x6~u,a,p!0),p(l!0) <-> \
			KaiC(x6~p,a,p!0),p(l!1),p(l!0,r!1) @ 'kpplus', 'kpminus'
#------------------------------------------------------------------------------

/*
conformational flipping as a function of phosphorylation state

it is important that: 1) in the absence of KaiB, the active state has a lower
free energy than the inactive one; 2) the hexamers should not flip at intermediate
phosphorylation levels.
*/

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
%var: 'd'  	  2		# h^{-1}  [original]
%var: 'g'  	  0.1 	# h^{-1}  [original]
%var: 'flipminus' 100	# h^{-1}  [original]
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

%var: 'flipzero' 10*'d'*('g'^6)
%var: 'flipone' 'd'*('g'^5)
%var: 'fliptwo' 'd'*('g'^4)
%var: 'flipthree' 'd'*('g'^3)
%var: 'flipfour' 'd'*('g'^2)
%var: 'flipfive' 'd'*('g'^1)
%var: 'flipsix' 'd'*('g'^0)
#------------------------------------------------------------------------------
'flip_0' 	KaiC(s~active,a,b1,b2,p!0),p(l!0,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r) \
			@ 'flipzero', 'flipminus'
'flip_1'	KaiC(s~active,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r) \
			@ 'flipone', 'flipminus'
'flip_2'	KaiC(s~active,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r) \
			@ 'fliptwo', 'flipminus'
'flip_3'	KaiC(s~active,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r) \
			@ 'flipthree', 'flipminus'
'flip_4'	KaiC(s~active,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r) \
			@ 'flipfour', 'flipminus'
'flip_5'	KaiC(s~active,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r) \
			@ 'flipfive', 'flipminus'
'flip_6'	KaiC(s~active,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r!6),p(l!6,r) <-> \
			KaiC(s~inactive,a,b1,b2,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r!6),p(l!6,r) \
			@ 'flipsix', 'flipminus'
#------------------------------------------------------------------------------

/*
KaiA un/binding to KaiC

(here only 1 KaiA binds to a KaiC hexamer)

The differential affinity mechanism requires that the affinity of KaiA for
KaiC be high, but decrease substantially as the phosphorylation level of the KaiC
hexamer increases.
*/

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
%var: 'kaplus'  	 1.72E+11 / 'stoch'		# molecules^{-1} h^{-1}  [original 1.72E+08] **
%var: 'kaminuszero'  	10 						# h^{-1}  [original]
%var: 'a'        3  					# [original]
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

#------------------------------------------------------------------------------
'A.Ca'	KaiC(a,s~active), KaiA(c,b) -> KaiC(a!1,s~active), KaiA(c!1,b) @ 'kaplus'
#------------------------------------------------------------------------------

#------------------------------------------------------------------------------
'A..Ca_0'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r) \
			@ 'kaminuszero'*('a'^0)
'A..Ca_1'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r!1),p(l!1,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r!1),p(l!1,r) \
			@ 'kaminuszero'*('a'^1)
'A..Ca_2'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r!1),p(l!1,r!2),p(l!2,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r!1),p(l!1,r!2),p(l!2,r) \
			@ 'kaminuszero'*('a'^2)
'A..Ca_3'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r) \
			@ 'kaminuszero'*('a'^3)
'A..Ca_4'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r) \
			@ 'kaminuszero'*('a'^4)
'A..Ca_5'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r) \
			@ 'kaminuszero'*('a'^5)
'A..Ca_6'	KaiC(a!11,s~active,p!0), KaiA(c!11), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r!6),p(l!6,r) -> \
			KaiC(a,s~active,p!0), KaiA(c), p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r!5),p(l!5,r!6),p(l!6,r) \
			@ 'kaminuszero'*('a'^6)
#------------------------------------------------------------------------------

/*
KaiA-facilitated phosphorylation. Note that KaiA is liberated in the process.
*/

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# This value is an experimental fit
#
%var: 'kpa'  	 2		# h^{-1}  [original 1]
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

'A..pC_1'	KaiC(a!11,x1~u,p!0),p(l!0), KaiA(c!11) -> \
			KaiC(a,x1~p,p!0),p(l!1),p(l!0,r!1), KaiA(c) \
			@ 'kpa'
'A..pC_2'	KaiC(a!11,x2~u,p!0),p(l!0), KaiA(c!11) -> \
			KaiC(a,x2~p,p!0),p(l!1),p(l!0,r!1), KaiA(c) \
			@ 'kpa'
'A..pC_3'	KaiC(a!11,x3~u,p!0),p(l!0), KaiA(c!11) -> \
			KaiC(a,x3~p,p!0),p(l!1),p(l!0,r!1), KaiA(c) \
			@ 'kpa'
'A..pC_4'	KaiC(a!11,x4~u,p!0),p(l!0), KaiA(c!11) -> \
			KaiC(a,x4~p,p!0),p(l!1),p(l!0,r!1), KaiA(c) \
			@ 'kpa'
'A..pC_5'	KaiC(a!11,x5~u,p!0),p(l!0), KaiA(c!11) -> \
			KaiC(a,x5~p,p!0),p(l!1),p(l!0,r!1), KaiA(c) \
			@ 'kpa'
'A..pC_6'	KaiC(a!11,x6~u,p!0),p(l!0), KaiA(c!11) -> \
			KaiC(a,x6~p,p!0),p(l!1),p(l!0,r!1), KaiA(c) \
			@ 'kpa'

/*
Two KaiB bind to inactive KaiC
*/

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
%var: 'kbadd0'  	 2.97E+10 / 'stoch'		# molecules^{-1} h^{-1} [original 2.97E+12]
%var: 'kbminus0'  	 1.0E+02 				# h^{-1}  [original]
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# 0-state is special

%var: 'BCionezero' 'kbadd0'*0.01
%var: 'BCitwozero' 'kbadd0'*0.01
%var: 'BCionezeroop' 'kbminus0'*10
%var: 'BCitwozeroop' 'kbminus0'*10

'B.Ci1_0'	KaiC(b1,s~inactive,p!0),p(l!0,r), KaiB(c) <-> \
			KaiC(b1!11,s~inactive,p!0),p(l!0,r), KaiB(c!11) \
			@ 'BCionezero','BCionezeroop'
'B.Ci2_0'	KaiC(b2,s~inactive,p!0),p(l!0,r), KaiB(c) <-> \
			KaiC(b2!11,s~inactive,p!0),p(l!0,r), KaiB(c!11) \
			@ 'BCitwozero','BCitwozeroop'

# the rest

'B.Ci1_g0'	KaiC(b1,s~inactive,p!0),p(l!0,r!_), KaiB(c) <-> \
			KaiC(b1!11,s~inactive,p!0),p(l!0,r!_), KaiB(c!11) \
			@ 'kbadd0','kbminus0'
'B.Ci2_g0'	KaiC(b2,s~inactive,p!0),p(l!0,r!_), KaiB(c) <-> \
			KaiC(b2!11,s~inactive,p!0),p(l!0,r!_), KaiB(c!11) \
			@ 'kbadd0','kbminus0'

/*
KaiA binds to KaiB on inactive KaiC

A variant of differential affinity.
*/

#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
%var: 'kbapluszero'  	 2.97E+18 / 'stoch'		# molecules^{-1} h^{-1}  [original]
%var: 'kbaminus'  	 	 1.0E+01 				# h^{-1}  [original 1.0E+02]  **

/* E+15 is good; */
#~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

#off

'A..B'		KaiB(a!1), KaiA(b!1) -> KaiB(a), KaiA(b) @ 'kbaminus'

# on

%var: 'cent' 'kbapluszero'*100
'A.B1_1'	KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'kbapluszero'
'A.B1_2'	KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'cent'
'A.B1_3'	KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'cent'
'A.B1_4'	KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b1!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'kbapluszero'
'A.B2_1'	KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'kbapluszero'
'A.B2_2'	KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'cent'
'A.B2_3'	KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'cent'
'A.B2_4'	KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r), KaiB(c!11,a), KaiA(c,b) -> \
			KaiC(b2!11,p!0),p(l!0,r!1),p(l!1,r!2),p(l!2,r!3),p(l!3,r!4),p(l!4,r), KaiB(c!11,a!12), KaiA(c,b!12) \
			@ 'kbapluszero'

/*
simulator directives
*/

%var: 'DIM_beat' 1. #
%var: 'DIM_length' 2 # clock beats

%var: 'DIM_tick' 0
%var: 'DIM_clock' 0
%mod: repeat [T] > 'DIM_clock' && 'DIM_tick' > 'DIM_length' - 1 do\
 $FLUX "flux_".'DIM_tick' - 'DIM_length'.".json" [false] until [false]
%mod: repeat [T] > 'DIM_clock' do $FLUX "flux_".'DIM_tick'.".json" "relative" [true]\
 until ('DIM_tick' + 'DIM_length' + 1) * 'DIM_beat' > [Tmax]
%mod: repeat [T] > 'DIM_clock' do\
 $UPDATE 'DIM_clock' 'DIM_clock' + 'DIM_beat';\
 $UPDATE 'DIM_tick' 'DIM_tick' + 1 until [false]
